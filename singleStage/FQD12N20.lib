*$
**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FQD12N20
** 200V N-Channel QFET MOSFET
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FQD12N20 2 1 3    
*Nom Temp=25 deg C
Dbody 7 5    DbodyMOD
Dbreak 5 11  DbreakMOD
Ebreak 11 7 17 7 220
Lgate 1 9    1.125e-9
Ldrain 2 5   1.440e-9
Lsource 3 7  9.664e-10
RLgate 1 9   11.25
RLdrain 2 5  14.4
RLsource 3 7 9.66
Rgate 9 6    1.5
It 7 17      1
Rbreak 17 7  RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8.28e-4 TC2=-1.13e-6)
.MODEL DbodyMOD D (IS=5.25e-9  N=1.5    RS=1.58e-2  TRS1=1.85e-3  TRS2=1.6e-7 
+ CJO=7.84e-10     M=0.56      VJ=0.68  TT=1.62e-7  XTI=3         EG=1.14)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1.0e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 0.21
.MODEL RdrainMOD RES (TC1=7.02e-3 TC2=1.35e-5)
M_BSIM3 16 6 7 7 Bsim3 W=1.48 L=2.0e-6 NRS=1
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=1000e-10    XJ=1.4e-6      NCH=1.13e17      
+ U0=700          VSAT=1.0e5     DROUT=1.0     
+ DELTA=0.1       PSCBE2=0       RSH=1.09e-3   
+ VTH0=4.55       VOFF=-0.1      NFACTOR=1.1
+ LINT=3.15e-7    DLC=3.15e-7    FC=0.5
+ CGSO=1.00e-15   CGSL=0         CGDO=8.05e-15 
+ CGDL=5.41e-10   CJ=0           CF=0
+ CKAPPA=0.2      KT1=-1.45      KT2=0
+ UA1=-1.28e-9    NJ=10)
.ENDS
*$
********************* Power Discrete MOSFET Thermal Model **********************
** Package: D-PAK / I-PAK
**------------------------------------------------------------------------------
.SUBCKT FQD12N20_Thermal TH TL
CTHERM1 TH 6 1.20e-5
CTHERM2 6 5  8.90e-4
CTHERM3 5 4  4.80e-3
CTHERM4 4 3  4.50e-2
CTHERM5 3 2  8.60e-2
CTHERM6 2 TL 2.20e-1 
RTHERM1 TH 6 8.20e-3
RTHERM2 6 5  4.20e-2
RTHERM3 5 4  3.65e-1
RTHERM4 4 3  4.45e-1
RTHERM5 3 2  5.60e-1
RTHERM6 2 TL 8.50e-1
.ENDS FQD12N20_Thermal 
**------------------------------------------------------------------------------
** Creation: Jul.-08-2014   Rev.: 1.0
** Fairchild Semiconductor
